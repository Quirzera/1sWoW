
&НаКлиенте
Процедура БроситьКубики(Команда)
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("Танк", ДляТанка);
	ПараметрыПолучения = Новый ФиксированнаяСтруктура(ПараметрыПолучения);
	ПолученноеПодземелье = БроситьКубикиНаСервере(ПараметрыПолучения);
	
	Сообщить("Тип: " + ТипЗнч(ПолученноеПодземелье) + " Знчение:" + ПолученноеПодземелье);
КонецПроцедуры

&НаСервереБезКонтекста
Функция БроситьКубикиНаСервере(Знач ФиксированныеПараметры, МодифицируемыеПараметры = Неопределено)
	Если ТипЗнч(МодифицируемыеПараметры) <> Тип("Структура") Тогда
		МодифицируемыеПараметры = Новый Структура();
	КонецЕсли;
	
	Танк = ФиксированныеПараметры.Танк;
	ТекущийСезон = Константы.WoW_ТекущийСезон.Получить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сезон", ТекущийСезон);
	ЗапросаТекст = "ВЫБРАТЬ
	|	WoW_СезонныеПодземелья.Подземелье
	|ИЗ
	|	РегистрСведений.WoW_СезонныеПодземелья КАК WoW_СезонныеПодземелья
	|ГДЕ
	|	WoW_СезонныеПодземелья.Сезон = &Сезон
	|	И &ОтборПоТанку";
	
	Если ЗначениеЗаполнено(Танк) Тогда
		ЗапросаТекст = СтрЗаменить(ЗапросаТекст, "&ОтборПоТанку", "WoW_СезонныеПодземелья.Танк = &Танк");
		Запрос.УстановитьПараметр("Танк", Танк);
	Иначе
		ЗапросаТекст = СтрЗаменить(ЗапросаТекст, "&ОтборПоТанку", "ИСТИНА");	
	КонецЕсли;
				
	Запрос.Текст = ЗапросаТекст;
	
	Подземелия = Запрос.Выполнить().Выгрузить();
	
	КоличествоПодземелий = Подземелия.Количество();
	
	Если КоличествоПодземелий = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Генератор = Новый ГенераторСлучайныхЧисел();
	ИскомыйИндекс = Генератор.СлучайноеЧисло(0, КоличествоПодземелий - 1);
				
	Возврат Подземелия[ИскомыйИндекс].Подземелье;
КонецФункции
